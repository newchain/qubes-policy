## <summary>qubes-gui-agent policy</summary>
## <desc>
##	<p>
##		Policy for qubes-gui-agent, the Qubes seemless gui frontend.
##	</p>
## </desc>


###############################
## <summary>
##	Connect to qubes-gui stream socket.
## </summary>
## <param name="domain">
##	<summary>
##		Domain to permit connection.
##	</summary>
## </param>

interface(`qubes_gui_stream_connect',`

	gen_require(`

		type qubes-gui_t;
	')

	allow $1 qubes-gui_t : unix_stream_socket connectto;
')


###############################
## <summary>
##	Write to qubes-gui stream socket.
## </summary>
## <param name="domain">
##	<summary>
##		Domain to permit write.
##	</summary>
## </param>

interface(`qubes_gui_stream_write',`

	gen_require(`

		type qubes-gui_var_run_t;
	')

	qubes_gui_stream_connect($1)

	allow $1 qubes-gui_var_run_t : sock_file write;
')


###############################
## <summary>
##	Inherit gui-agent fds
## </summary>
## <param name="domain">
##	<summary>
##		Domain to permit inheritance.
##	</summary>
## </param>

interface(`qubes_gui_use_fds',`

	gen_require(`

		type qubes-gui_t;
	')

	allow $1 qubes-gui_t : fd use;
')


###############################
## <summary>
##	Read and open Qubes xserver config.
## </summary>
## <param name="domain">
##	<summary>
##		Domain to permit read.
##	</summary>
## </param>

interface(`qubes_gui_read_qubes_gui_etc',`

	gen_require(`

		type qubes-gui_etc_t;
	')

	allow $1 qubes-gui_etc_t : file { read open getattr };
')
